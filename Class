public class CompanyDescriptionController {

    public Account acc { get; set; } 
    public Account_Planning__c accPlan { get; set; } 
    public List<TaskWrapper> taskList { get; set; }
    public Integer rowIndex { get; set; }
    
    public CompanyDescriptionController(ApexPages.StandardController stdController) {
        stdController.addFields(new List<String>{
            'Tier_Level__c', 
            'Business_Description__c'
        });
        this.acc = (Account)stdController.getRecord();
        
        List<Account_Planning__c> existingPlans = [
            SELECT Id, Tier__c, Top_Prospect__c, Top_Client__c,
                   Primary_NAICS__c, Primary_NAICS_Description__c, ABL__c,
                   Ownership__c, Business_Description__c, Strategic_Opportunity__c
            FROM Account_Planning__c
            WHERE Account__c = :acc.Id
            ORDER BY CreatedDate DESC
            LIMIT 1
        ];

        if (!existingPlans.isEmpty()) {
            // A plan was found, so we load it for editing.
            this.accPlan = existingPlans[0];
            
            // ============== NEW LOGIC ==============
            // Query for existing tasks related to this plan
            List<Task> relatedTasks = [
                SELECT Subject, Description, ActivityDate 
                FROM Task 
                WHERE WhatId = :accPlan.Id
            ];
            
            this.taskList = new List<TaskWrapper>();
            
            // Loop through the existing tasks and create wrapper objects to display them
            for (Task t : relatedTasks) {
                TaskWrapper wrapper = new TaskWrapper(taskList.size());
                wrapper.taskRec = t;
                wrapper.isEditMode = false; // Existing tasks should be read-only initially
                this.taskList.add(wrapper);
            }
            // =======================================

        } else {
            // No plan was found, so we create a new one.
            this.accPlan = new Account_Planning__c();
            this.accPlan.Tier__c = acc.Tier_Level__c;
            this.accPlan.Business_Description__c = acc.Business_Description__c;
            
            // Initialize an empty task list for the new plan
            this.taskList = new List<TaskWrapper>();
        }
    }

    public void saveAccountDetails() {
        if (acc.Id == null) {
            ApexPages.addMessage(new ApexPages.Message(ApexPages.Severity.ERROR, 'An Account ID is required to save.'));
            return;
        }
        try {
            accPlan.Account__c = acc.Id;
            acc.Business_Description__c = accPlan.Business_Description__c;
            upsert accPlan; 
            update acc;
            ApexPages.addMessage(new ApexPages.Message(ApexPages.Severity.CONFIRM, 'Account Plan was saved successfully.'));
        } catch (DmlException e) {
            ApexPages.addMessages(e);
        }
    }

    public PageReference openPDF() {
        PageReference pdfPage = Page.CompanyDescriptionView;
        pdfPage.getParameters().put('id', acc.Id);
        pdfPage.setRedirect(true);
        return pdfPage;
    }
    
    public void saveRow() {
        if (rowIndex != null && rowIndex < taskList.size()) {
            TaskWrapper wrapper = taskList.get(rowIndex);
            if (accPlan.Id == null) {
                ApexPages.addMessage(new ApexPages.Message(ApexPages.Severity.ERROR, 'You must save the main Account Plan details before adding tasks.'));
                return;
            }
            wrapper.taskRec.WhatId = accPlan.Id;
            Date startDate = Date.newInstance(2025, 9, 25);
            Date endDate = Date.newInstance(2025, 10, 5);
            Date dueDate = wrapper.taskRec.ActivityDate;
            if (dueDate != null && (dueDate <= startDate || dueDate >= endDate)) {
                ApexPages.addMessage(new ApexPages.Message(ApexPages.Severity.ERROR, 'Outside the valid Account Planning Period'));
                taskList.remove(rowIndex);
                return; 
            }
            if (String.isBlank(wrapper.taskRec.Subject) || 
                String.isBlank(wrapper.taskRec.Description) || 
                dueDate == null) {
                ApexPages.addMessage(new ApexPages.Message(ApexPages.Severity.ERROR, 'Subject, Description, and Date are all required.'));
                return;
            }
            try {
                upsert wrapper.taskRec;
                wrapper.isEditMode = false;
            } catch (DmlException e) {
                ApexPages.addMessages(e);
            }
        }
    }

    public Boolean getIsActionPlanDisabled() {
        return accPlan.Id == null;
    }

    public void addRow() {
        if (taskList.size() < 5) {
            taskList.add(new TaskWrapper(taskList.size()));
        }
    }
    public void editRow() {
        if (rowIndex != null && rowIndex < taskList.size()) {
            taskList.get(rowIndex).isEditMode = true;
        }
    }
    public Boolean getIsAddDisabled() {
        return taskList.size() >= 5;
    }
    public class TaskWrapper {
        public Task taskRec { get; set; }
        public Boolean isEditMode { get; set; }
        public Integer rowIndex { get; set; }
        public TaskWrapper(Integer index) {
            this.taskRec = new Task();
            this.isEditMode = true;
            this.rowIndex = index;
        }
    }
}
